prototype(CodeQ.Instagram:ImagesList) < prototype(Neos.Fusion:Component) {
	limit = 6
	# currently get images returns up to 24 items as first page
	# but since not all feed items have a media_url or a thumbnail_url
	# consider getting a second page when you need more then ~10 images.
	collection = ${Array.filter(Instagram.getFeed(), item => item.thumbnail_url || item.media_url)}
	attributes = Neos.Fusion:Attributes

	renderer = afx`
		<Neos.Fusion:Tag tagName="ul" attributes={props.attributes}>
			<Neos.Fusion:Loop items={Array.slice(props.collection, 0, props.limit)} itemName="item" @children="itemRenderer">
				<CodeQ.Instagram:Image item={item} />
			</Neos.Fusion:Loop>
		</Neos.Fusion:Tag>
	`

	@cache {
		mode = 'cached'
		maximumLifetime = ${Instagram.getTokenLifetime()}
	}
}
